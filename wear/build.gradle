buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.google.gms:google-services:4.3.5'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.4.1'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.8.2'
    }
}


apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.github.triplet.play'


repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    google()
}

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

if (keystorePropertiesFile.canRead()) {
// Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    throw new GradleException("Could not read keystore.properties!")
}

android {

    signingConfigs {
        releaseConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file("spacelaunchnow.keystore")
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    defaultConfig {
        flavorDimensions "release"
        manifestPlaceholders = [applicationLabel: "@string/app_name"]
        applicationId "me.calebjones.spacelaunchnow"
        minSdkVersion rootProject.wearMinSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionName computeVersionName()
        versionCode computeVersionCode()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.releaseConfig
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug-b${defaultConfig.versionCode}"
            manifestPlaceholders = [applicationLabel: "SLN - Debug"]
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def computeVersionName() {
    return String.format('%d.%d.%d', app.versionMajor, app.versionMinor, app.versionPatch)
}

// Will return 120042 for Jenkins build #42
def computeVersionCode() {
    // Major + minor + Jenkins build number (where available)
    return 2000000 + app.versionMajor * 10000 + app.versionMinor * 1000 + app.versionPatch * 100 + app.versionCode
}

play {
    serviceAccountCredentials = rootProject.file("publisher-key.json")
    track = "internal"
    releaseStatus = "completed"
    commit = false
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(path: ':data')
    implementation deps.butterknife
    implementation 'com.android.support:wear:28.0.0'
    annotationProcessor deps.butterknifeCompiler
    compileOnly deps.supportWearable
    implementation deps.crashlytics
    //Networking
    implementation deps.retrofit
    implementation deps.converter_gson
    implementation deps.gson
    implementation deps.okhttp
    implementation deps.easyprefs
    implementation deps.evernoteJob
    implementation deps.wear
    implementation deps.palette
    implementation deps.percentlayout
    implementation deps.appcompat
    implementation deps.legacySupportv4
    implementation deps.media
    implementation deps.supportWearable
    implementation deps.playWearable
    implementation deps.timber
    implementation deps.materialValue
    implementation deps.inAppbilling
    implementation deps.constraintLayout
    compileOnly deps.wearable
    compileOnly 'com.google.android.wearable:wearable:2.7.0'
}
