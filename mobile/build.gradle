buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.21.4'
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'realm-android'
apply plugin: 'com.getkeepsafe.dexcount'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

advancedVersioning {
    nameOptions {
        versionMajor 1
        versionMinor 5
        versionPatch 1

    }
    codeOptions {
        versionCodeType org.moallemi.gradle.internal.VersionCodeType.AUTO_INCREMENT_ONE_STEP
        dependsOnTasks 'release'
    }
    outputOptions {
    }
}

def appVersionName = advancedVersioning.versionName
def appVersionCode = advancedVersioning.versionCode

def renameAPK(variant, defaultConfig, buildType) {
    variant.outputs.each { output ->
        def formattedDate = new Date().format('yyyyMMdd')

        def file = output.packageApplication.outputFile
        def fileName = applicationName + "-" + buildType + "-" + defaultConfig.versionName + "." + defaultConfig.versionCode + "-" + formattedDate + "-" + 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim() + ".apk"
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
}

android {

    signingConfigs {
        config {
            keyAlias "${alias}"
            keyPassword "${password}"
            storeFile file('/Users/cjones/Google Drive/Dev/BlogSite.keystore')
            storePassword "${password}"
        }
    }
    compileSdkVersion 25
    buildToolsVersion '24.0.2'
    defaultConfig {
        applicationId "me.calebjones.spacelaunchnow"
        manifestPlaceholders = [appNameLabel                   : "@string/application_name",
                                manifestApplicationId          : "${applicationId}",
                                onesignal_app_id               : "${app_id}",
                                onesignal_google_project_number: "${google_project_number}"]
        minSdkVersion 16
        targetSdkVersion 25
        versionCode appVersionCode
        versionName appVersionName
        signingConfig signingConfigs.config
        resConfigs "en"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            manifestPlaceholders = [appNameLabel         : "SLN - Debug",
                                    manifestApplicationId: "me.calebjones.spacelaunchnow.debug"]
        }
        release {
//            minifyEnabled true
//            shrinkResources true
//            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                if (variant.buildType.name == "release") {
                    renameAPK(variant, defaultConfig, "release")
                }
            }
        }
    }
    productFlavors {
    }

    lintOptions{
        abortOnError false
    }
}

dependencies {

    wearApp project(':wear')
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile('com.mikepenz:aboutlibraries:5.6.5@aar') {
        transitive = true
    }

    compile('com.github.afollestad.material-dialogs:core:0.9.0.1@aar') { transitive = true }
    compile 'com.github.afollestad.material-dialogs:commons:0.9.0.1@aar'
    compile "com.android.support:customtabs:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:palette-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:support-v4:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    apt 'com.jakewharton:butterknife-compiler:8.4.0'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile "com.google.android.gms:play-services-location:$rootProject.playServicesVersion"
    compile "com.google.android.gms:play-services-wearable:$rootProject.playServicesVersion"
    compile "com.google.android.gms:play-services-gcm:$rootProject.playServicesVersion"
    compile 'com.onesignal:OneSignal:3.4.0@aar'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    compile 'com.github.clans:fab:1.6.4'
    compile 'za.co.riggaroo:materialhelptutorial:1.1.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.borax12.materialdaterangepicker:library:1.9'
    compile 'net.mediavrog:integrated-rating-request:1.1.2'
    compile 'com.jakewharton.timber:timber:4.3.1'
    compile 'com.simplecityapps:recyclerview-fastscroll:1.0.11'
    compile 'com.github.javiersantos:MaterialStyledDialogs:1.4.1'
    compile 'com.karumi:dexter:2.3.1'
    compile 'com.anjlab.android.iab.v3:library:1.0.32'
    compile 'hanks.xyz:smallbang-library:0.1.2'
    compile 'com.github.amlcurran.showcaseview:library:5.4.3'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.github.renaudcerrato:static-maps-api:1.0.2'
    compile 'com.github.pwittchen:weathericonview:1.1.0'
    compile 'android.zetterstrom.com.forecast:forecast:1.2.0'
    compile 'it.macisamuele:calendarprovider-lib:0.0.1'
    compile 'com.github.michael-rapp:android-preference-activity:4.2.8'
    compile 'com.evernote:android-job:1.1.0'
    compile 'blue.aodev:material-values:1.1.0'
}
